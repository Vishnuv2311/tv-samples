/*
 * Copyright 2020 Google LLC
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

include ':app'
rootProject.name = "TV Reference App"

// Link in a locally cloned repo of the AndroidX Media3 project if available
// (see https://github.com/androidx/media#locally also):
// ```
// # mkdir -p /home/$USER/github/androidx/
// # cd /home/$USER/github/androidx/
// # git clone https://github.com/androidx/media.git
// ```
//
// Set `androidx.media3.dir` to the path of the root dir of the media3
// repository in your `local.properties`:
// ```
// androidx.media3.dir=/home/$USER/github/androidx/media
// ```
File localPropertyFile = new File(rootDir.toString() + '/local.properties')
if (localPropertyFile.canRead()) {
    Properties properties = new Properties()
    properties.load(localPropertyFile.newDataInputStream())
    String androidxMediaSourceRepositoryDir = properties.getProperty("androidx.media3.dir", null)
    if (androidxMediaSourceRepositoryDir != null) {
        gradle.ext.androidxMediaModulePrefix = 'media3-'
        apply from: file("$androidxMediaSourceRepositoryDir/core_settings.gradle")
    }
}